plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'maven-publish'

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
    }
}

compileJava.options.fork = true
def jdk16Home = System.getenv("JDK_16")
if (jdk16Home == null) {
    throw new GradleException('JDK_16 environment variable is not defined')
}
compileJava.options.forkOptions.executable = "$jdk16Home/bin/javac"

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

dependencies {
    compileOnly 'jaxen:jaxen:1.1.6'
}

archivesBaseName = 'jdom'

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.java
}

artifacts {
    archives sourceJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'org.jetbrains.intellij.deps'
            artifactId 'jdom'
            version projectVersion
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publish = true

    pkg {
        repo = 'intellij-third-party-dependencies'
        name = 'jdom'
        userOrg = 'jetbrains'

        version {
            name = projectVersion
        }
    }

    publications = ['MyPublication']
}
